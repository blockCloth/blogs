在文章开始之前，我们可以先来了解下什么是字节码：
Java 的字节码指令由操作码和操作数组成：

- 操作码（Opcode）：一个字节长度（0-255，意味着指令集的操作码总数不可能超过 256 条），代表着某种特定的操作含义。
- 操作数（Operands）：零个或者多个，紧跟在操作码之后，代表此操作需要的参数。

由于 Java 虚拟机是基于栈而不是寄存器的结构，所以大多数字节码指令都只有一个操作码。比如 `**aload_0 **`就只有操作码没有操作数，而 `**invokespecial #1**` 则由操作码和操作数组成。

- **aload_0**：将局部变量表中下标为 0 的数据压入操作数栈中
- **invokespecial #1**：调用成员方法或者构造方法，并传递常量池中下标为 1 的常量

字节码指令主要有以下几种，分别是：

- 加载与存储指令
- 算术指令
- 类型转换指令
- 对象的创建与访问指令
- 方法调用和返回指令
- 操作数栈管理指令
- 控制转移指令
## 加载与存储指令
加载（load）和存储（store）指令是使用最频繁的指令，用于将数据从**栈帧的局部变量表和操作数栈**之间来回传递。
### 示例代码以及字节码
```java
public int add(int a, int b) {
    int result = a + b;
    return result;
}

public int add(int, int);
    Code:
       0: iload_1
       1: iload_2
       2: iadd
       3: istore_3
       4: ireturn

```
#### 执行过程
通过执行过程来分析`load`和`store`指令的过程
![](https://cdn.nlark.com/yuque/0/2024/png/22796888/1717035334159-f73274d6-82a8-4f70-82e4-d88862da6176.png#averageHue=%23f5eee8&clientId=u49270400-3c55-4&from=paste&height=1071&id=ud4f35414&originHeight=1770&originWidth=1736&originalType=url&ratio=1.25&rotation=0&showTitle=false&status=done&style=none&taskId=u0aeeb1a1-0b93-4684-a9bd-68d20ca34db&title=&width=1050)
### 将局部变量表中的变量压入操作数栈中

- `**xload_<n>**`（x 为 i、l、f、d、a，n 默认为 0 到 3），表示将第 n 个局部变量压入操作数栈中。
- `**xload**`（x 为 i、l、f、d、a），通过指定参数的形式，将局部变量压入操作数栈中，当使用这个指令时，表示局部变量的数量可能超过了 4 个
#### 操作助记符
| **Java 类型** | **描述符** |
| --- | --- |
| 基本数据类型 int | i |
| 基本数据类型 long | j |
| 基本数据类型 float | f |
| 基本数据类型 double | d |
| 基本数据类型 boolean | z |
| 基本数据类型 char | c |
| 基本数据类型 short | s |
| 基本数据类型 byte | c |
| 引用数据类型 | a |

#### 示例代码
```java
private void load(int age, String name, long birthday, boolean sex) {
    System.out.println(age + name + birthday + sex);
}
```
通过 **jclasslib** 可以查看到 **load** 方法（4 个参数）的字节码指令：
```java
iload_1  // 将局部变量表中下标为 1 的 int 变量压入操作数栈中
aload_2  // 将局部变量表中下标为 2 的引用数据类型变量（此时为 String）压入操作数栈中
lload_3  // 将局部变量表中下标为 3 的 long 型变量压入操作数栈中
iload 5  // 将局部变量表中下标为 5 的 int 变量（实际为 boolean）压入操作数栈中
```
通过局部变量表计能查看到对应关系了：
![image.png](https://cdn.nlark.com/yuque/0/2024/png/22796888/1717036429179-b8e0889e-256d-4634-b21e-c4939900f0ee.png#averageHue=%23252a32&clientId=u7fedc9e0-4a7c-4&from=paste&height=469&id=u8f453cc3&originHeight=586&originWidth=1250&originalType=binary&ratio=1.25&rotation=0&showTitle=false&size=55137&status=done&style=none&taskId=u03016253-aa26-472a-bcfb-8e226704f6b&title=&width=1000)
#### 特殊指令：arraylength
像 **arraylength** 指令，没有操作码助记符，它没有代表数据类型的特殊字符，但操作数只能是一个数组类型的对象。
#### 大多数指令对基本数据类型的处理
大部分的字节码指令不支持 **byte**、**short** 和 **char** 类型，甚至没有任何指令直接支持 **boolean** 类型。编译器会将这些类型的数据转换为 **int** 类型：

- **byte** 和 **short** 类型的数据带符号扩展（Sign-Extend）为 **int** 类型。
- **boolean** 和 **char** 类型的数据零位扩展（Zero-Extend）为 **int** 类型。
### 将常量池中的常量压入操作数栈中
在 Java 字节码中，根据数据类型和入栈内容的不同，常量入栈指令可以细分为 const 系列、push 系列和 ldc（Load Constant）指令。
#### 常量入栈指令概述

1. **const 系列**：用于将特殊的常量值直接入栈。常量值隐含在指令本身。
2. **push 系列**：用于将较小范围的整数值入栈。包括 **bipush** 和 **sipush**：
   - **bipush**：接收 8 位整数作为参数，范围为 [-128, 127]。
   - **sipush**：接收 16 位整数作为参数，范围为 [-32768, 32767]。
3. **ldc 系列**：用于将常量池中的常量值入栈。适用于超出 **const** 和 **push** 指令范围的值。
   - **ldc**：接收一个 8 位的参数，指向常量池中的索引。
   - **ldc_w**：接收两个 8 位数，索引范围更大。
   - **ldc2_w**：用于 long 或 double 类型的常量。
#### 示例代码
通过具体的代码示例进一步解释常量入栈指令的作用：
```java
public void pushConstLdc() {
    int iconst = -1;  // 范围 [-1,5]
    int bipush = 127; // 范围 [-128,127]
    int sipush = 32767; // 范围 [-32768,32767]
    int ldc = 32768;  // 其他 int
    String aconst = null;
    String IdcString = "沉默王二";
}
```
通过 **jclasslib** 查看 **pushConstLdc** 方法的字节码指令：
```java
iconst_m1       // 将 -1 入栈。范围 [-1,5]
bipush 127      // 将 127 入栈。范围 [-128,127]
sipush 32767    // 将 32767 入栈。范围 [-32768,32767]
ldc #6 <32768>  // 将常量池中下标为 6 的常量 32768 入栈
aconst_null     // 将 null 入栈
ldc #7 <沉默王二> // 将常量池中下标为 7 的常量“沉默王二”入栈
```
#### const详解
在 Java 字节码中，const 系列指令用于将常量值直接压入操作数栈。每个 const 系列指令对应一个特定的常量值，这些常量值隐含在指令本身。以下是 const 系列指令及其对应的常量值的范围表：

| **指令名称** | **操作码（Opcode）** | **常量值** |
| --- | --- | --- |
| **iconst_m1** | **0x02** | -1 |
| **iconst_0** | **0x03** | 0 |
| **iconst_1** | **0x04** | 1 |
| **iconst_2** | **0x05** | 2 |
| **iconst_3** | **0x06** | 3 |
| **iconst_4** | **0x07** | 4 |
| **iconst_5** | **0x08** | 5 |
| **lconst_0** | **0x09** | 0L |
| **lconst_1** | **0x0A** | 1L |
| **fconst_0** | **0x0B** | 0.0f |
| **fconst_1** | **0x0C** | 1.0f |
| **fconst_2** | **0x0D** | 2.0f |
| **dconst_0** | **0x0E** | 0.0d |
| **dconst_1** | **0x0F** | 1.0d |
| **aconst_null** | **0x01** | null 引用 |

##### 解释与示例
**1. iconst 系列指令：**用于将指定范围内的 **int** 常量值（-1 至 5）直接压入操作数栈。例如：

- **iconst_m1** 将 **-1** 压入操作数栈。
- **iconst_0** 将 **0** 压入操作数栈。
- **iconst_1** 将 **1** 压入操作数栈。

**2. lconst 系列指令：**用于将 **long** 常量值 **0L** 和 **1L** 直接压入操作数栈。例如：

- **lconst_0** 将 **0L** 压入操作数栈。
- **lconst_1** 将 **1L** 压入操作数栈。

**3. fconst 系列指令：**用于将 **float** 常量值 **0.0f**、**1.0f** 和 **2.0f** 直接压入操作数栈。例如：

- **fconst_0** 将 **0.0f** 压入操作数栈。
- **fconst_1** 将 **1.0f** 压入操作数栈。
- **fconst_2** 将 **2.0f** 压入操作数栈。

**4. dconst 系列指令：**用于将 **double** 常量值 **0.0d** 和 **1.0d** 直接压入操作数栈。例如：

- **dconst_0** 将 **0.0d** 压入操作数栈。
- **dconst_1** 将 **1.0d** 压入操作数栈。

**5. aconst_null 指令：**用于将 **null** 引用直接压入操作数栈。例如：

- **aconst_null** 将 **null** 压入操作数栈。
### 将栈顶的数据出栈并装入局部变量表中
在 Java 字节码中，局部变量赋值指令主要以 **store** 的形式存在，用于将栈顶的数据出栈并装入局部变量表中。这些指令用于给局部变量赋值。

- **xstore_<n>**（x 为 i、l、f、d、a，n 默认为 0 到 3）：将栈顶的值存储到局部变量表中索引为 **n** 的位置。
- **xstore**（x 为 i、l、f、d、a）：通过指定参数的形式，将栈顶的值存储到局部变量表中。当使用这个指令时，表示局部变量的数量可能超过了 4 个。
#### xstore_<n> 和 xstore 的区别

- **xstore_<n>**：仅占用 1 个字节，适用于局部变量表中前几个位置的变量。
- **xstore n**：由操作码和操作数组成，操作码占 1 个字节，操作数占 2 个字节，一共占 3 个字节，适用于数量超过 4 个的局部变量。
#### 示例代码
```java
public void store(int age, String name) {
    int temp = age + 2;
    String str = name;
}
```
使用 **jclasslib** 查看 **store** 方法的字节码指令：
```java
0: iload_1        // 将局部变量表中索引为 1 的 int 变量压入操作数栈
1: iconst_2       // 将常量 2 压入操作数栈
2: iadd           // 将栈顶的两个 int 值相加，并将结果压入操作数栈
3: istore_3       // 从操作数栈中弹出一个整数，并把它赋值给局部变量表中索引为 3 的变量
4: aload_2        // 将局部变量表中索引为 2 的引用类型变量压入操作数栈
5: astore 4       // 从操作数栈中弹出一个引用数据类型，并把它赋值给局部变量表中索引为 4 的变量
```
## 算术指令
算术指令用于对操作数栈上的值进行特定运算，并将结果重新压入操作数栈中。这些指令分为两类：**整型数据**的运算指令和**浮点数据**的运算指令。
**Java 字节码中的算术指令包括以下几类：**

- **加法指令**：iadd、ladd、fadd、dadd
- **减法指令**：isub、lsub、fsub、dsub
- **乘法指令**：imul、lmul、fmul、dmul
- **除法指令**：idiv、ldiv、fdiv、ddiv
- **求余指令**：irem、lrem、frem、drem
- **自增指令**：iinc

这些指令的命名规则是：

- **i** 表示 **int**
- **l** 表示 **long**
- **f** 表示 **float**
- **d** 表示 **double**
### 示例代码
我们通过一个简单的 Java 代码示例来展示这些算术指令的使用和执行过程。
```java
public void calculate(int age) {
    int add = age + 1;
    int sub = age - 1;
    int mul = age * 2;
    int div = age / 3;
    int rem = age % 4;
    age++;
    age--;
}
```
使用 **jclasslib** 查看 **calculate** 方法的字节码指令：
```
0: iload_1        // 将局部变量表中索引为 1 的 int 变量 age 压入操作数栈
1: iconst_1       // 将常量 1 压入操作数栈
2: iadd           // 将栈顶的两个 int 值相加，并将结果压入操作数栈
3: istore_2       // 将操作数栈顶的值存储到局部变量表中索引为 2 的位置 add

4: iload_1        // 将 age 压入操作数栈
5: iconst_1       // 将常量 1 压入操作数栈
6: isub           // 将栈顶的两个 int 值相减，并将结果压入操作数栈
7: istore_3       // 将操作数栈顶的值存储到局部变量表中索引为 3 的位置 sub

8: iload_1        // 将 age 压入操作数栈
9: iconst_2       // 将常量 2 压入操作数栈
10: imul          // 将栈顶的两个 int 值相乘，并将结果压入操作数栈
11: istore_4      // 将操作数栈顶的值存储到局部变量表中索引为 4 的位置 mul

12: iload_1       // 将 age 压入操作数栈
13: iconst_3      // 将常量 3 压入操作数栈
14: idiv          // 将栈顶的两个 int 值相除，并将结果压入操作数栈
15: istore_5      // 将操作数栈顶的值存储到局部变量表中索引为 5 的位置 div

16: iload_1       // 将 age 压入操作数栈
17: iconst_4      // 将常量 4 压入操作数栈
18: irem          // 将栈顶的两个 int 值求余，并将结果压入操作数栈
19: istore_6      // 将操作数栈顶的值存储到局部变量表中索引为 6 的位置 rem

20: iinc 1, 1     // 将局部变量表中索引为 1 的 int 变量 age 自增 1
23: iinc 1, -1    // 将局部变量表中索引为 1 的 int 变量 age 自减 1

```
### 特殊情况：溢出、Infinity 和 NaN
需要注意的是，算术运算可能会导致溢出。例如，两个很大的正整数相加可能会得到一个负数。Java 虚拟机规范并未对这种情况给出具体结果，因此程序不会显式报错。
当发生溢出时，将会使用有符号的无穷大 **Infinity** 来表示；如果某个操作结果没有明确的数学定义，将会使用 **NaN**（Not a Number）值来表示。
#### 示例代码
```java
public void infinityNaN() {
    int i = 10;
    double j = i / 0.0;
    System.out.println(j); // Infinity
    
    double d1 = 0.0;
    double d2 = d1 / 0.0;
    System.out.println(d2); // NaN
}
```

- 任何一个非零的数除以浮点数 0，结果是无穷大 **Infinity**。
- 当被除数为 0 时，结果用 **NaN** 值表示。
### 运算模式
Java 虚拟机提供了两种运算模式：

1. **向最接近数舍入**：在进行浮点数运算时，所有的结果都必须舍入到一个适当的精度，不是特别精确的结果必须舍入为可被表示的最接近的精确值（类似四舍五入）
2. **向零舍入**：将浮点数转换为整数时，采用该模式选择一个最接近但不大于原值的数字作为最精确的舍入结果（类似取整）。
## 类型转换指令
在 Java 字节码中，类型转换指令用于在不同数据类型之间进行转换。类型转换指令可以分为两种：宽化转换和窄化转换。
### 类型转换指令概述

1. **宽化转换**：小类型向大类型转换。
   - 从 **int** 到 **long**、**float**、**double**：i2l、i2f、i2d
   - 从 **long** 到 **float**、**double**：l2f、l2d
   - 从 **float** 到 **double**：f2d
2. **窄化转换**：大类型向小类型转换。
   - 从 **int** 到 **byte**、**short**、**char**：i2b、i2s、i2c
   - 从 **long** 到 **int**：l2i
   - 从 **float** 到 **int**、**long**：f2i、f2l
   - 从 **double** 到 **int**、**long**、**float**：d2i、d2l、d2f
#### 宽化转换

- **不会发生精度丢失**：从 **int** 到 **long** 或 **double**
- **可能会发生精度丢失**：从 **int**、**long** 到 **float、**从 **long** 到 **double**
- **隐式发生的宽化类型转换**：从 **byte**、**char** 和 **short** 到 **int**
#### 窄化转换

- **可能发生精度丢失**：从 **int** 到 **byte**、**short** 或 **char、**从 **long** 到 **int、**从 **float** 到 **int** 或 **long、**从 **double** 到 **int**、**long** 或 **float**
### 示例代码与字节码分析
我们通过一个简单的 Java 代码示例来展示这些类型转换指令的使用和执行过程。
```java
public void updown() {
    int i = 10;
    double d = i;
    
    float f = 10f;
    long ong = (long) f;
}
```
使用 **jclasslib** 查看 **updown** 方法的字节码指令：
```latex
0: bipush        10      // 将常量 10 压入操作数栈
1: istore_1              // 将栈顶的 int 值存储到局部变量表索引为 1 的位置

2: iload_1               // 将局部变量表索引为 1 的 int 值压入操作数栈
3: i2d                   // 将 int 值转换为 double 值
4: dstore_2              // 将栈顶的 double 值存储到局部变量表索引为 2 的位置

5: ldc           #2      // 将常量 10.0 压入操作数栈
6: fstore_4              // 将栈顶的 float 值存储到局部变量表索引为 4 的位置

7: fload_4               // 将局部变量表索引为 4 的 float 值压入操作数栈
8: f2l                   // 将 float 值转换为 long 值
9: lstore_5              // 将栈顶的 long 值存储到局部变量表索引为 5 的位置

```
## 对象的创建和访问指令
### 创建指令
数组是一种特殊的对象，它创建的字节码指令和普通对象的创建指令不同。创建数组的指令有三种：

- **newarray**：创建基本数据类型的数组
- **anewarray**：创建引用类型的数组
- **multianewarray**：创建多维数组

而对象的创建指令只有一个，就是 new，它会接收一个操作数，指向常量池中的一个索引，表示要创建的类型。
#### 示例代码
```java
public void newObject() {
    String name = new String("沉默王二");
    File file = new File("无愁河的浪荡汉子.book");
    int [] ages = {};
}
```
使用 **jclasslib** 查看 **updown** 方法的字节码指令：
```java
 0 new #15 <java/lang/String> //创建一个String对象
 3 dup
 4 ldc #13 <沉默王二>
 6 invokespecial #16 <java/lang/String.<init> : (Ljava/lang/String;)V>
 9 astore_1
10 new #17 <java/io/File> //创建一个File 对象
13 dup
14 ldc #18 <无愁河的浪荡汉子.book>
16 invokespecial #19 <java/io/File.<init> : (Ljava/lang/String;)V>
19 astore_2
20 iconst_0
21 newarray 10 (int)  //创建一个int数组
23 astore_3
24 return

```
### 字段访问指令
**字段**可以分为两类，一类是成员变量，一类是静态变量（也就是类变量），所以字段访问指令可以分为两类：

1. **访问静态变量**：
   - **getstatic**：获取静态变量的值。
   - **putstatic**：设置静态变量的值。
2. **访问成员变量（需要创建对象后才能访问）**：
   - **getfield**：获取成员变量的值。
   - **putfield**：设置成员变量的值。
#### 示例代码与字节码分析
我们通过一个简单的 Java 代码示例来展示这些字段访问指令的使用和执行过程。
##### 示例代码
```java
public class Writer {
    private String name;
    static String mark = "作者";

    public static void main(String[] args) {
        print(mark);
        Writer w = new Writer();
        print(w.name);
    }

    public static void print(String arg) {
        System.out.println(arg);
    }
}
```
使用 **jclasslib** 查看 **main** 方法的字节码指令：
```java
getstatic #2 <com/github/dyx/stack/Writer.mark : Ljava/lang/String;> //访问静态变量 mark
getfield #6 <com/github/dyx/stack/Writer.name : Ljava/lang/String;> //访问静态字段 name
```
## 方法调用和返回指令
在 Java 字节码中，方法调用指令有 5 种，分别用于不同的场景。方法调用和返回指令是字节码中非常重要的一部分，帮助我们理解方法的执行过程。
### 方法调用指令概述

1. **invokevirtual**：用于调用对象的成员方法，根据对象的实际类型进行分派，支持多态。
2. **invokeinterface**：用于调用接口方法，会在运行时搜索由特定对象实现的接口方法进行调用。
3. **invokespecial**：用于调用一些需要特殊处理的方法，包括构造方法、私有方法和父类方法。
4. **invokestatic**：用于调用静态方法。
5. **invokedynamic**：用于在运行时动态解析出调用点限定符所引用的方法，并执行。
### 示例代码与字节码分析
我们通过一个简单的 Java 代码示例来展示这些方法调用指令的使用和执行过程。
```java
import java.util.ArrayList;
import java.util.List;

public class InvokeExamples {
    private void run() {
        List<String> ls = new ArrayList<>();
        ls.add("难顶");

        ArrayList<String> als = new ArrayList<>();
        als.add("学不动了");
    }

    public static void print() {
        System.out.println("invokestatic");
    }

    public static void main(String[] args) {
        print();
        InvokeExamples invoke = new InvokeExamples();
        invoke.run();
    }
}
```
使用 **javap -c InvokeExamples.class** 查看字节码指令：
**InvokeExamples** 类有 4 个方法，包括缺省的构造方法在内。
```java
Compiled from "InvokeExamples.java"
public class com.github.dyx.stack.InvokeExamples
    public com.github.dyx.stack.InvokeExamples();
        descriptor: ()V
        flags: (0x0001) ACC_PUBLIC
        Code:
          stack=1, locals=1, args_size=1
             0: aload_0
             1: invokespecial #1                  // Method java/lang/Object."<init>":()V
             4: return
          LineNumberTable:
            line 6: 0
          LocalVariableTable:
            Start  Length  Slot  Name   Signature
                0       5     0  this   Lcom/github/dyx/stack/InvokeExamples;
    
      private void run();
        descriptor: ()V
        flags: (0x0002) ACC_PRIVATE
        Code:
          stack=2, locals=3, args_size=1
             0: new           #2                  // class java/util/ArrayList
             3: dup
             4: invokespecial #3                  // Method java/util/ArrayList."<init>":()V
             7: astore_1
             8: aload_1
             9: ldc           #4                  // String 难顶
            11: invokeinterface #5,  2            // InterfaceMethod java/util/List.add:(Ljava/lang/Object;)Z
            16: pop
            17: new           #2                  // class java/util/ArrayList
            20: dup
            21: invokespecial #3                  // Method java/util/ArrayList."<init>":()V
            24: astore_2
            25: aload_2
            26: ldc           #6                  // String 学不动了
            28: invokevirtual #7                  // Method java/util/ArrayList.add:(Ljava/lang/Object;)Z
            31: pop
            32: return
          LineNumberTable:
            line 8: 0
            line 9: 8
            line 11: 17
            line 12: 25
            line 13: 32
          LocalVariableTable:
            Start  Length  Slot  Name   Signature
                0      33     0  this   Lcom/github/dyx/stack/InvokeExamples;
                8      25     1    ls   Ljava/util/List;
               25       8     2   als   Ljava/util/ArrayList;
    
      public static void print();
        descriptor: ()V
        flags: (0x0009) ACC_PUBLIC, ACC_STATIC
        Code:
          stack=2, locals=0, args_size=0
             0: getstatic     #8                  // Field java/lang/System.out:Ljava/io/PrintStream;
             3: ldc           #9                  // String invokestatic
             5: invokevirtual #10                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
             8: return
          LineNumberTable:
            line 16: 0
            line 17: 8
    
      public static void main(java.lang.String[]);
        descriptor: ([Ljava/lang/String;)V
        flags: (0x0009) ACC_PUBLIC, ACC_STATIC
        Code:
          stack=2, locals=2, args_size=1
             0: invokestatic  #11                 // Method print:()V
             3: new           #12                 // class com/github/dyx/stack/InvokeExamples
             6: dup
             7: invokespecial #13                 // Method "<init>":()V
            10: astore_1
            11: aload_1
            12: invokespecial #14                 // Method run:()V
            15: return
          LineNumberTable:
            line 20: 0
            line 21: 3
            line 22: 11
            line 23: 15
          LocalVariableTable:
            Start  Length  Slot  Name   Signature
                0      16     0  args   [Ljava/lang/String;
               11       5     1 invoke   Lcom/github/dyx/stack/InvokeExamples;
    }
SourceFile: "InvokeExamples.java"
```
### 方法调用指令的具体含义

1. **invokespecial**
- 用于调用一些需要特殊处理的方法，包括构造方法、私有方法和父类方法。
```
invokespecial #1 // Method java/lang/Object."<init>":()V
```

2. **invokeinterface 和 invokevirtual**
- **invokeinterface** 用于调用接口方法。
```
invokeinterface #5,  2  // InterfaceMethod java/util/List.add:(Ljava/lang/Object;)Z
```

- **invokevirtual** 用于调用对象的成员方法。
```
invokevirtual #7 // Method java/util/ArrayList.add:(Ljava/lang/Object;)Z
```

3. **invokestatic**
- 用于调用静态方法。
```
invokestatic  #11 // Method print:()V
```

4. **invokedynamic**
- 用于在运行时动态解析出调用点限定符所引用的方法，并执行。此指令在 Java 7 中引入，主要用于支持动态语言和 Lambda 表达式。
### Lambda 表达式示例
```java
import java.util.function.Function;

public class LambdaExample {
    public static void main(String[] args) {
        Function<Integer, Integer> square = x -> x * x;
        int result = square.apply(5);
        System.out.println(result); // 输出 25
    }
}
```
使用 **javap -c LambdaExample.class** 查看字节码指令：
```java
 public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: (0x0009) ACC_PUBLIC, ACC_STATIC
    #2 = InvokeDynamic      #0:#40         // #0:apply:()Ljava/util/function/Function;
    Code:
      stack=2, locals=3, args_size=1
         0: invokedynamic #2,  0              // InvokeDynamic #0:apply:()Ljava/util/function/Function;
         5: astore_1
         6: aload_1
         7: iconst_5
         8: invokestatic  #3                  // Method java/lang/Integer.valueOf:(I)Ljava/lang/Integer;
        11: invokeinterface #4,  2            // InterfaceMethod java/util/function/Function.apply:(Ljava/lang/Object;)Ljava/lang/Object;
        16: checkcast     #5                  // class java/lang/Integer
        19: invokevirtual #6                  // Method java/lang/Integer.intValue:()I
        22: istore_2
        23: getstatic     #7                  // Field java/lang/System.out:Ljava/io/PrintStream;
        26: iload_2
        27: invokevirtual #8                  // Method java/io/PrintStream.println:(I)V
        30: return
```

- `**invokedynamic #2, 0**`：使用 `invokedynamic` 调用一个引导方法，这个方法负责实现并返回一个 Function 接口的实例。这里的 Lambda 表达式 `**x -> x * x **`被转换成了一个 Function 对象。引导方法在首次执行时会被调用，它负责生成一个 CallSite，该 CallSite 包含了指向具体实现 Lambda 表达式的方法句柄（Method Handle）。在这个例子中，这个方法句柄指向了 `lambda$main$0 `方法。
- Lambda 表达式的实现是：`lambda$main$0`，这是 Lambda 表达式 `x -> x * x` 的实际实现。它接收一个 Integer 对象作为参数，计算其平方，然后返回结果。
### 返回指令
![](https://cdn.nlark.com/yuque/0/2024/png/22796888/1717054017385-53d1d2b5-f30b-4878-be10-a9fe4a378093.png#averageHue=%23f2f2f2&clientId=u3e9adebb-4d88-4&from=paste&height=613&id=u458a91a3&originHeight=630&originWidth=938&originalType=url&ratio=1.25&rotation=0&showTitle=false&status=done&style=none&taskId=uc91cf009-7240-43d6-be91-83846c2fc6f&title=&width=912)
## 操作数栈管理指令
常见的操作数栈管理指令有 pop、dup 和 swap。

- 将一个或两个元素从栈顶弹出，并且直接废弃，比如 pop，pop2；
- 复制元素：
   - **dup**：复制栈顶的一个元素并将其重新压入栈顶。
   - **dup2**：复制栈顶的两个元素并将其重新压入栈顶。
   - **dup_x1**：复制栈顶的一个元素，并将其插入到栈顶下面一个元素的位置。
   - **dup2_x1**：复制栈顶的两个元素，并将其插入到栈顶下面一个元素的位置。
   - **dup_x2**：复制栈顶的一个元素，并将其插入到栈顶下面两个元素的位置。
   - **dup2_x2**：复制栈顶的两个元素，并将其插入到栈顶下面两个元素的位置。
- 将栈最顶端的两个槽中的数值交换位置，比如 swap。

这些指令不需要指明数据类型，因为是按照位置压入和弹出的。
### 示例代码与字节码分析
我们通过一个简单的 Java 代码示例来展示这些操作数栈管理指令的使用和执行过程。
#### 示例代码
```java
public class Dup {
    int age;
    public int incAndGet() {
        return ++age;
    }
}
```
使用 **jclasslib** 查看 **incAndGet** 方法的字节码指令：
```
0: aload_0           // 将 this 入栈
1: dup               // 复制栈顶的 this
2: getfield #2       // 将常量池中下标为 2 的常量加载到栈上，同时将一个 this 出栈
5: iconst_1          // 将常量 1 入栈
6: iadd              // 将栈顶的两个值相加后出栈，并将结果放回栈上
7: dup_x1            // 复制栈顶的元素，并将其插入 this 下面
8: putfield #2       // 将栈顶的两个元素出栈，并将其赋值给字段 age
11: ireturn          // 将栈顶的元素出栈返回
```
## 控制转移指令
Java 字节码中的控制转移指令用于实现程序的流程控制。这些指令包括比较指令、条件跳转指令、比较条件跳转指令、多条件分支跳转指令、无条件跳转指令，以及异常处理和同步指令。理解这些指令有助于掌握 Java 虚拟机的工作原理和程序的执行过程。
### 比较指令
比较指令用于比较栈顶的两个元素的大小，并将比较结果入栈。常见的比较指令有：**dcmpg**，**dcmpl**、**fcmpg**、**fcmpl**、**lcmp**。注意，没有int类型。

- 对于 **double** 和 **float** 类型，由于 NaN 的存在，有两个版本的比较指令。拿 **float** 来说，有 **fcmpg** 和 **fcmpl**，区别在于，如果遇到 NaN，**fcmpg** 会将 1 压入栈，**fcmpl** 会将 -1 压入栈。
#### 示例代码
```java
public void lcmp(long a, long b) {
    if (a > b) {}
}
```
使用 **jclasslib** 查看 **lcmp** 方法的字节码指令：
```
0 lload_1
1 lload_3
2 lcmp  //用于两个long类型的数据比较
3 ifle 6 (+3)
6 return
```
### 条件跳转指令
条件跳转指令接收两个字节的操作数，测试栈顶元素是否满足某一条件，满足的话跳转到对应位置。常见的条件跳转指令有 **ifeq**、**ifne**、**iflt**、**ifge**、**ifgt**、**ifle**。
这些指令都会接收两个字节的操作数，它们的统一含义是，弹出栈顶元素，测试它是否满足某一条件，满足的话，跳转到对应位置。

- 对于 long、float 和 double 类型的条件分支比较，会先执行比较指令返回一个整型值到操作数栈中后再执行 int 类型的条件跳转指令。
- 对于 **boolean、byte、char、short**，以及 int，则直接使用条件跳转指令来完成。
| **指令** | **说明** |
| --- | --- |
| ifeq | 等于 0 时跳转 |
| ifne | 不等于 0 时跳转 |
| iflt | 小于 0 时跳转 |
| ifle | 小于等于 0 时跳转 |
| ifgt | 大于 0 时跳转 |
| ifge | 大于等于 0 时跳转 |
| ifnull | 为 null 时跳转 |
| ifnonnull | 不为 null 时跳转 |

#### 示例代码
```java
public void fi() {
    int a = 0;
    if (a == 0) {
        a = 10;
    } else {
        a = 20;
    }
}
```
使用 **jclasslib** 查看 **fi** 方法的字节码指令：
```
 0 iconst_0
 1 istore_1
 2 iload_1
 3 ifne 12 (+9)   //如果栈顶的元素不等于 0，跳转到第 12（3+9）行 12 bipush 20
 6 bipush 10
 8 istore_1
 9 goto 15 (+6)
12 bipush 20
14 istore_1
15 return
```
### 比较条件转指令
比较条件转指令将比较和跳转两个步骤合二为一。常见的指令有 **if_icmpeq**、**if_icmpne**、**if_icmplt**、**if_icmpge**、**if_icmpgt**、**if_icmple**。前缀“if_”后，以字符“i”开头的指令针对 int 型整数进行操作，以字符“a”开头的指令表示对象的比较。

| **指令** | **说明** |
| --- | --- |
| if_icmpeq | 比较栈顶两个 int 类型的数值大小，相等时跳转 |
| if_icmpne | 比较栈顶两个 int 类型的数值大小，不等时跳转 |
| if_icmplt | 比较栈顶两个 int 类型的数值大小，前者小于后者时跳转 |
| if_icmple | 比较栈顶两个 int 类型的数值大小，前者小于等于后者时跳转 |
| if_icmpgt | 比较栈顶两个 int 类型的数值大小，前者大于后者时跳转 |
| if_icmpge | 比较栈顶两个 int 类型的数值大小，前者大于等于后者时跳转 |
| if_acmpeq | 比较栈顶两个引用类型的大小，相等时跳转 |
| if_acmpne | 比较栈顶两个引用类型的大小，不相等时跳转 |

#### 示例代码
```java
public void compare() {
    int i = 10;
    int j = 20;
    System.out.println(i > j);
}
```
使用 **jclasslib** 查看 **compare** 方法的字节码指令：
```
 0 bipush 10
 2 istore_1
 3 bipush 20
 5 istore_2
 6 getstatic #3 <java/lang/System.out : Ljava/io/PrintStream;>
 9 iload_1
10 iload_2
11 if_icmple 18 (+7) //如果栈顶的两个int 类型的数值比较的话，若前者小于后者是跳转到第18 行（11 + 7）
14 iconst_1
15 goto 19 (+4)
18 iconst_0
19 invokevirtual #20 <java/io/PrintStream.println : (Z)V>
22 return
```
### 多条件分支跳转指令
主要有 **tableswitch** 和 **lookupswitch**，前者要求多个条件分支值是连续的，它内部只存放起始值和终止值，以及若干个跳转偏移量，通过给定的操作数 index，可以立即定位到跳转偏移量位置，因此效率比较高；后者内部存放着各个离散的 case-offset 对，每次执行都要搜索全部的 case-offset 对，找到匹配的 case 值，并根据对应的 offset 计算跳转地址，因此效率较低。
#### 示例代码
```java
public void switchTest(int select) {
    int num;
    switch (select) {
        case 1:
            num = 10;
            break;
        case 2:
        case 3:
            num = 30;
            break;
        default:
            num = 40;
    }
}

```
使用 **jclasslib** 查看 **switchTest** 方法的字节码指令：
```
 0 iload_1
 1 tableswitch 1 to 3
	1:  28 (+27)
	2:  34 (+33)
	3:  34 (+33)
	default:  40 (+39)
28 bipush 10
30 istore_2
31 goto 43 (+12)
34 bipush 30
36 istore_2
37 goto 43 (+6)
40 bipush 40
42 istore_2
43 return
```
case 2 的时候没有 break，所以 case 2 和 case 3 是连续的，用的是 **tableswitch**。如果等于 1，跳转到 28 行；如果等于 2 和 3，跳转到 34 行，如果是 default，跳转到 40 行。
### 无条件跳转指令
**goto** 指令接收两个字节的操作数，共同组成一个带符号的整数，用于指定指令的偏移量，指令执行的目的就是跳转到偏移量给定的位置处。
前面的例子里都出现了 **goto** 的身影，也很好理解。如果指令的偏移量特别大，超出了两个字节的范围，可以使用指令 **goto_w**，接收 4 个字节的操作数。
## 异常处理时的字节码指令
通过一个简单的 Java 代码示例说明异常处理时的字节码指令。
#### 示例代码
```java
public class ExceptionExample {
    public void testException() {
        try {
            int a = 1 / 0; // 这将导致除以零的异常
        } catch (ArithmeticException e) {
            System.out.println("发生算术异常");
        }
    }
}
```
使用 **javap -c ExceptionExample** 查看其字节码：
```
public void testException();
  Code:
    0: iconst_1
    1: iconst_0
    2: idiv
    3: istore_1
    4: goto 12
    7: astore_1
    8: getstatic     #2  // Field java/lang/System.out:Ljava/io/PrintStream;
    11: ldc           #3  // String 发生算术异常
    13: invokevirtual #4  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
    16: return
  Exception table:
    from    to  target type
       0     4     7   Class java/lang/ArithmeticException
```
#### 字节码指令分析

- **iconst_1**、**iconst_0**、**idiv**：执行除法运算 **1 / 0**。
- **astore_1**：将异常对象存储到局部变量表。
- **getstatic**、**ldc**、**invokevirtual**：执行 **System.out.println("发生算术异常")**。
- **goto 12**：跳过 catch 块的代码。
## synchronized 的字节码指令
同步代码块的示例：
#### 示例代码
```java
public class SynchronizedExample {
    public void syncBlockMethod() {
        synchronized(this) {
            // 同步块体
        }
    }
}
```
使用 **jclasslib** 查看 **syncBlockMethod** 方法的字节码指令：
```
public void syncBlockMethod();
  Code:
    0: aload_0
    1: dup
    2: astore_1
    3: monitorenter
    4: aload_1
    5: monitorexit
    6: goto          14
    9: astore        2
    11: aload_1
    12: monitorexit
    13: aload         2
    15: athrow
    16: return
  Exception table:
    from    to  target type
        4     6     9   any
        9    13     9   any
```
#### 字节码指令分析

- **monitorenter**：获取对象的监视器锁。
- **monitorexit**：释放锁。
- **Exception table**：定义异常处理器。
